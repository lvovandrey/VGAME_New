; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "VGame"
#define MyAppVersion "1.0.1.2"
#define MyAppPublisher "Lvov A.A."
#define MyAppExeName "VanyaGame.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{D5805AB1-233A-4937-BB54-8EA3D60FD737}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
LicenseFile=License.rtf
OutputDir=I:\VGame
OutputBaseFilename=VGameSetup_{#MyAppVersion}
SetupIconFile=C:\Users\Professional\Pictures\book-icon.ico
Compression=lzma
SolidCompression=yes
UninstallDisplayIcon={app}\uninstall-icon.ico
UninstallDisplayName ={#MyAppName}

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "ttsvoice"; Description: "Установка голоса Elena от RHVoice (распространяется бесплатно по лицензии GPL v3)"

[Files]
Source: "I:\VGame\Union\VanyaGame.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "I:\VGame\Union\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "I:\VGame\Resourses\*"; DestDir: "{localappdata}\VGame"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "I:\VGame\InstallationTools\*"; DestDir: "{tmp}"; Flags: ignoreversion recursesubdirs createallsubdirs deleteafterinstall
Source: "I:\VGame\dotNet4.5.2.exe"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall; Check: IsRequiredDotNetDetectedSilence
Source: "I:\VGame\RHVoice-voice-Russian-Elena-v4.2.9-setup.exe"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall;  Tasks: ttsvoice
Source: "I:\VGame\vc_redist.x86.exe"; DestDir: "{tmp}"; Flags: ignoreversion deleteafterinstall;  


[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\VanyaGame.exe"
Name: "{group}\Редактор БД карточек"; Filename: "{app}\CardsEditor.exe"
Name: "{group}\Удалить VGame"; Filename: "{uninstallexe}"; IconFilename: "{app}\uninstall-icon.ico"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\VanyaGame.exe"; Tasks: desktopicon
Name: "{commondesktop}\Редактор БД карточек"; Filename: "{app}\CardsEditor.exe"; Tasks: desktopicon

[Run]
Filename: "{tmp}\dotNet4.5.2.exe"; Check: IsRequiredDotNetDetected
Filename: "{tmp}\vc_redist.x86.exe";
Filename: "{tmp}\RHVoice-voice-Russian-Elena-v4.2.9-setup.exe"; Tasks: ttsvoice
Filename: "{tmp}\InstallationTools.exe"; Parameters: """{localappdata}\VGame\VGame.Config.xml"" ""{localappdata}\VGame\CardsEditor.Config.xml"" ""{localappdata}\VGame"" ""{localappdata}\VGame\Data\Fruits.db"""

[UninstallDelete]
Type: filesandordirs; Name: "{localappdata}\VGame\*"

[Code]
//-----------------------------------------------------------------------------
//  Проверка наличия нужного фреймворка
//-----------------------------------------------------------------------------
function IsDotNetDetected(version: string; release: cardinal): boolean;

var 
    reg_key: string; // Просматриваемый подраздел системного реестра
    success: boolean; // Флаг наличия запрашиваемой версии .NET
    release45: cardinal; // Номер релиза для версии 4.5.x
    key_value: cardinal; // Прочитанное из реестра значение ключа
    sub_key: string;

begin

    success := false;
    reg_key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\';
    
    // Вресия 3.0
    if Pos('v3.0', version) = 1 then
      begin
          sub_key := 'v3.0';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'InstallSuccess', key_value);
          success := success and (key_value = 1);
      end;

    // Вресия 3.5
    if Pos('v3.5', version) = 1 then
      begin
          sub_key := 'v3.5';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Install', key_value);
          success := success and (key_value = 1);
      end;

     // Вресия 4.0 клиентский профиль
     if Pos('v4.0 Client Profile', version) = 1 then
      begin
          sub_key := 'v4\Client';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Install', key_value);
          success := success and (key_value = 1);
      end;

     // Вресия 4.0 расширенный профиль
     if Pos('v4.0 Full Profile', version) = 1 then
      begin
          sub_key := 'v4\Full';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Install', key_value);
          success := success and (key_value = 1);
      end;

     // Вресия 4.5
     if Pos('v4.5', version) = 1 then
      begin
          sub_key := 'v4\Full';
          reg_key := reg_key + sub_key;
          success := RegQueryDWordValue(HKLM, reg_key, 'Release', release45);
          success := success and (release45 >= release);
      end;

    result := success;

end;


//-----------------------------------------------------------------------------
//  Функция-обертка для детектирования конкретной нужной нам версии
//-----------------------------------------------------------------------------
function IsRequiredDotNetDetected(): boolean;
begin

  if not IsDotNetDetected('v4.5', 379893) then
    begin
    if MsgBox('{#MyAppName} требует установки Microsoft .NET Framework 4.5.2'#13#13
              'На Вашем компьютере данный пакет не установлен.'#13#13
              'Вы можете установить его с помощью данного инсталлятора сейчас или позже самостоятельно с сайта Microsoft.'#13#13
              'Установить .NET Framework 4.5.2 прямо сейчас? ', mbConfirmation, MB_YESNO) = IDYES then
        begin
            result:= true;
            Exit;
        end;       
   end; 
   result := false;
end;

//-----------------------------------------------------------------------------
//  Функция-обертка для детектирования конкретной нужной нам версии
//-----------------------------------------------------------------------------
function IsRequiredDotNetDetectedSilence(): boolean;
begin
  result:= not IsDotNetDetected('v4.5', 379893); 
end;

//-----------------------------------------------------------------------------
//    Callback-функция, вызываемая при инициализации установки
//-----------------------------------------------------------------------------
function InitializeSetup(): boolean;
begin
  result := true;
end;
